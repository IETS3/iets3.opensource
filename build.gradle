import de.itemis.mps.gradle.GenerateLibrariesXml
import de.itemis.mps.gradle.GitBasedVersioning

import java.time.LocalDateTime


//will pull the groovy classes/types from nexus to the classpath
buildscript {
    if(JavaVersion.current() != JavaVersion.VERSION_1_8){
        throw new GradleException("This build script requires java " + JavaVersion.VERSION_1_8 + ", but you are currently using " + JavaVersion.current())
    }
    repositories {
        maven { url 'https://projects.itemis.de/nexus/content/repositories/mbeddr' }
    }
    dependencies {
        classpath 'de.itemis.mps:mps-gradle-plugin:1.0.55.678ba7a'
    }
}

plugins {
    id "co.riiid.gradle" version "0.4.2"
}

// detect if we are in a CI build
if (project.hasProperty("forceCI")) {
    ext.ciBuild = true
} else {
    //on teamcity we are in a CI build
    ext.ciBuild = project.hasProperty("teamcity")
}


def forceLocal = project.hasProperty("forceLocalDependencies")


// Dependency versions
ext.mpsVersion = '2018.3-RC1'

def mbeddrVersion = "1.2+"

// if building a against a special branch from mbeddr is required add the name here
// the name is enough no trailing "." is required, also the plain name from git can
// be used here. No need to convert "/" the script will take care of that.
def mbeddrBranch = "mps/20183"


if (mbeddrBranch != null && !mbeddrBranch.trim().isEmpty()) {
    ext.mbeddrVersionSelector = "${mbeddrBranch.replace("/", "-")}.${mbeddrVersion}"
} else {
    ext.mbeddrVersionSelector = mbeddrVersion
}


// Project version
if (project.hasProperty('iets3OpenSourceVersion')) {
    version = project.iets3OpenSourceVersion
} else {
    if (ciBuild) {
        version = GitBasedVersioning.getVersionWithCount("0", "1", System.env.BUILD_NUMBER.toInteger())
        println "##teamcity[buildNumber '${version}']"
    } else {
        println "Local build detected, version will be SNAPSHOT"
        version = "1.0-SNAPSHOT"
    }
}

if (!project.hasProperty("mbeddrVersion")) {
    if (forceLocal) {
        logger.log(LogLevel.WARN, "Local SNAPSHOT version forced")
        ext.mbeddrVersionSelector = '1.0-SNAPSHOT'
    }
} else {
    logger.log(LogLevel.WARN, "mbeddr version externally overwritten to $ext.mbeddrVersion")
    ext.mbeddrVersionSelector = ext.mbeddrVersion
}


ext.releaseRepository = 'https://projects.itemis.de/nexus/content/repositories/mbeddr'
ext.snapshotRepository = 'https://projects.itemis.de/nexus/content/repositories/mbeddr_snapshots'
ext.publishingRepository = version.toString().endsWith("-SNAPSHOT") ? snapshotRepository : releaseRepository

ext.dependencyRepositories = [
    'https://projects.itemis.de/nexus/content/repositories/mbeddr',
    'https://projects.itemis.de/nexus/content/repositories/mbeddr_snapshots'
]

ext.artifactsDir = new File(rootDir, 'artifacts')

apply plugin: 'maven-publish'
apply plugin: 'base'


task wrapper(type: Wrapper) {
    gradleVersion '4.4.1'
    distributionType 'all'
}

configurations {
    mps
    mpsArtifacts
}

dependencies {
    mps "com.jetbrains:mps:$mpsVersion"
    mpsArtifacts "com.mbeddr:platform:$mbeddrVersionSelector"
}

repositories {
        mavenLocal()
        for (repoUrl in project.dependencyRepositories) {
            maven {
                url repoUrl
                if (project.hasProperty('nexusUsername')) {
                    credentials {
                        username project.nexusUsername
                        password project.nexusPassword
                    }
                }
            }
        }
}

task resolveMps(type: Copy) {
    dependsOn configurations.mps
    from {
        configurations.mps.resolve().collect { zipTree(it) }
    }
    into "$buildDir/mps"
}

task resolveMpsArtifacts(type: Copy) {
    from {
        configurations.mpsArtifacts.resolve().collect { zipTree(it) }
    }
    into artifactsDir
}

ant.properties['mps.home'] = resolveMps.destinationDir
ant.properties['iets3.github.opensource.home'] = rootDir
ant.properties['build.dir'] = rootDir
ant.properties['artifacts.root'] = resolveMpsArtifacts.destinationDir
ant.properties['version'] = version
ant.importBuild('code/languages/build.xml') { target -> 'ant-' + target }

tasks['ant-check-mps-home'].dependsOn resolveMps
tasks['ant-clean-and-build'].dependsOn resolveMps, resolveMpsArtifacts

task packageAllScripts(type: Zip, dependsOn: 'ant-clean-and-build') {
    baseName 'org.iets3.opensource.allScripts'
    from artifactsDir
    include 'org.iets3.opensource.allScripts.build/**'
}

task packageOpenSource(type: Zip, dependsOn: 'ant-clean-and-build') {
    baseName 'org.iets3.opensource'
    from artifactsDir
    include 'org.iets3.opensource/**'
}

task packageTests(type: Zip, dependsOn: 'ant-clean-and-build') {
    baseName 'org.iets3.opensource.tests'
    from new File(artifactsDir, 'org.iets3.opensource.tests')
}

assemble.dependsOn packageAllScripts, packageOpenSource, packageTests

publishing {
    repositories {
        maven {
            url project.publishingRepository
            if (project.hasProperty('nexusUsername')) {
                credentials {
                    username project.nexusUsername
                    password project.nexusPassword
                }
            }
        }
    }
    publications {
        allScripts(MavenPublication) {
            groupId 'org.iets3.opensource'
            artifactId 'allScripts'
            artifact packageAllScripts
        }
        openSource(MavenPublication) {
            groupId 'org.iets3'
            artifactId 'opensource'
            artifact packageOpenSource
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.mpsArtifacts.resolvedConfiguration.firstLevelModuleDependencies.each{
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.moduleGroup)
                    dependencyNode.appendNode('artifactId', it.moduleName)
                    dependencyNode.appendNode('version', it.moduleVersion)
                    dependencyNode.appendNode('type', it.moduleArtifacts[0].type)
                }
                configurations.mps.resolvedConfiguration.firstLevelModuleDependencies.each{
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.moduleGroup)
                    dependencyNode.appendNode('artifactId', it.moduleName)
                    dependencyNode.appendNode('version', it.moduleVersion)
                    dependencyNode.appendNode('type', it.moduleArtifacts[0].type)
                    dependencyNode.appendNode('scope', 'provided')
                }
            }
        }
        tests(MavenPublication) {
            groupId 'org.iets3.opensource'
            artifactId 'tests'
            artifact packageTests
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.mps.resolvedConfiguration.firstLevelModuleDependencies.each{
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.moduleGroup)
                    dependencyNode.appendNode('artifactId', it.moduleName)
                    dependencyNode.appendNode('version', it.moduleVersion)
                    dependencyNode.appendNode('type', it.moduleArtifacts[0].type)
                }
            }
        }
    }
}

// Ant <junit> task support
repositories {
    mavenCentral()
}
configurations {
    junitAnt
}
dependencies {
    junitAnt 'junit:junit:4.12'
    junitAnt('org.apache.ant:ant-junit:1.9.7') {
        transitive = false
    }
    junitAnt('org.apache.ant:ant-junit4:1.9.7') {
        transitive = false
    }
}
ant.taskdef(name: 'junit', classname: 'org.apache.tools.ant.taskdefs.optional.junit.JUnitTask',
        classpath: configurations.junitAnt.asPath)
ant.taskdef(name: 'junitreport', classname: 'org.apache.tools.ant.taskdefs.optional.junit.XMLResultAggregator',
        classpath: configurations.junitAnt.asPath)

task patchHeapSize4TestsExec {
    doLast {
        def antTestScriptFile = rootProject.file('build/iets3.opensource.tests/build.xml')
        antTestScriptFile.text = antTestScriptFile.text.replace("-Xmx1024m", "-Xmx2048m")
    }
}
tasks['ant-build-and-run-tests'].dependsOn patchHeapSize4TestsExec

check.dependsOn 'ant-build-and-run-tests'

task generateLibrariesXml(type: GenerateLibrariesXml) {
    dependsOn resolveMpsArtifacts
    description "Will read project libraries from projectlibraries.properties and generate libraries.xml in .mps directory. Libraries are loaded in mps during start."
    defaults rootProject.file('projectlibraries.properties')
    overrides rootProject.file('projectlibraries.overrides.properties')
    destination file('code/languages/org.iets3.opensource/.mps/libraries.xml')
}

task setup {
    dependsOn generateLibrariesXml
    dependsOn resolveMpsArtifacts
    description 'Set up MPS project libraries. Libraries are read in from projectlibraries.properties file.'
}

defaultTasks 'ant-clean-and-build'

LocalDateTime t = LocalDateTime.now()

def releaseNotes = """Automated Nighly build from ${t as String}."""

github {
    owner = 'IETS3'
    repo = 'iets3.opensource'
    token = rootProject.hasProperty("github.token") ? rootProject.getProperty("github.token") : "empty"
    tagName = 'nightly-release-' + version
    targetCommitish = GitBasedVersioning.getGitCommitHash()
    name = 'Nighly Build ' + version
    body = releaseNotes
    prerelease = true
    assets = packageOpenSource.outputs.files
}

githubRelease.dependsOn packageOpenSource
